;╔═════════════════════════════════════════════════════════════════════════════════════════════════
;║     about_ui.pbi                                                                           
;╠═════════════════════════════════════════════════════════════════════════════════════════════════
;║     Created: 11-08-2025 
;║
;║     Copyright (c) 2025 James Dooley <james@dooley.ch>
;║
;║     History:
;║     11-08-2025: Initial version
;╚═════════════════════════════════════════════════════════════════════════════════════════════════
DeclareModule AboutUI
  
  Declare.b ShowAboutDialog(hWindow.i) ; Displays the about dialog
  
  Declare.s AboutUIError() ; Returns last error generated by the library

EndDeclareModule

Module AboutUI
  EnableExplicit
  
  UseModule Consts
  
  UsePNGImageDecoder()
  
  #DLG_Width = 350
  #Dlg_Height = 130
  
  #DLG_Image_Size = 128
  
  #DLG_X_Column1 = #DLG_DEFAULT_GADGET_PADDING * 3 ; Image X
  #DLG_X_Column2 = 100 ; Labels X
  #DLG_X_Column3 = (#DLG_Width - #DLG_DEFAULT_BUTTON_WIDTH) / 2; OK Button X
  
  #DLG_Y_Row1 = #DLG_DEFAULT_GADGET_PADDING * 3 ; Image & Title Y
  #DLG_Y_Row2 = #DLG_Y_Row1 + #DLG_DEFAULT_BUTTON_HEIGHT ; Version Label Y
  #DLG_Y_Row3 = #DLG_Y_Row2 + #DLG_DEFAULT_BUTTON_HEIGHT ; Copyright Label Y
  #DLG_Y_Row4 = #Dlg_Height - #DLG_DEFAULT_BUTTON_HEIGHT - (#DLG_DEFAULT_GADGET_PADDING * 2) ; OK Button Y
  
  #DLG_Label_Width = #DLG_Width - #DLG_X_Column2 - (#DLG_DEFAULT_GADGET_PADDING * 3)
  
  Define lastError$ = ""
  Define.i hMainWindow, hDialog
  
  Procedure OnCloseDialog()
    Shared hMainWindow, hDialog
    
    If IsWindow(hDialog)
      CloseWindow(hDialog)
    EndIf
    
    DisableWindow(hMainWindow, #False)
    SetActiveWindow(hMainWindow)
  EndProcedure
  
  ;┌───────────────────────────────────────────────────────────────────────────────────────────────
  ;│     Public     
  ;└───────────────────────────────────────────────────────────────────────────────────────────────

  ; Returns last error generated by the library
  Procedure.s AboutUIError()
    Shared lastError$
    ProcedureReturn lastError$
  EndProcedure
  
  ; Displays the about dialog
  ;
  ; Params
  ; hWindow - The ID of the application's main window
  ;
  Procedure.b ShowAboutDialog(hWindow.i) 
    Shared lastError$, hMainWindow, hDialog
    Protected hLogo.i, hOkButton.i
    lastError$ = #Empty$
    
    If IsWindow(hWindow)
      hMainWindow = hWindow
      
      hDialog = OpenWindow(#PB_Any, #PB_Default, #PB_Default, #DLG_Width, #Dlg_Height, "About " + #APP_TITLE$, #DLG_WINDOW_FLAGS, WindowID(hMainWindow))
      If IsWindow(hDialog)
        ; Add the gadgets
        hLogo = CatchImage(#PB_Any, ?AboutLogo)
        
        ImageGadget(#PB_Any, #DLG_X_Column1, #DLG_Y_Row1, #DLG_Image_Size, #DLG_Image_Size, ImageID(hLogo))
        TextGadget(#PB_Any, #DLG_X_Column2, #DLG_Y_Row1, #DLG_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, #APP_TITLE$, #PB_Text_Center)
        TextGadget(#PB_Any, #DLG_X_Column2, #DLG_Y_Row2, #DLG_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, "Version: " + #APP_VERSION$, #PB_Text_Center)
        TextGadget(#PB_Any, #DLG_X_Column2, #DLG_Y_Row3, #DLG_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, #APP_COPYRIGHT_NOTICE, #PB_Text_Center)
        hOkButton = ButtonGadget(#PB_Any, #DLG_X_Column3, #DLG_Y_Row4, #DLG_DEFAULT_BUTTON_WIDTH, #DLG_DEFAULT_BUTTON_HEIGHT, "Ok", #PB_Button_Default)
        
        BindEvent(#PB_Event_CloseWindow, @OnCloseDialog(), hDialog)
        BindGadgetEvent(hOkButton, @OnCloseDialog(), #PB_EventType_LeftClick)
        
        ; Stop processing on the main window
        DisableWindow(hMainWindow, #True)
        
        ProcedureReturn #True
      EndIf
      
      lastError$ = "Failed to create About dialog window"
      ProcedureReturn #False      
    EndIf
    
    lastError$ = "Invalid window ID passed to function: ShowAboutDialog"
    ProcedureReturn #False
  EndProcedure
  
  DataSection
    AboutLogo:
    IncludeBinary #PB_Compiler_FilePath + "res/about/app-icon@" + #DLG_Image_Size + "px.png"
  EndDataSection
EndModule
; IDE Options = PureBasic 6.21 - C Backend (MacOS X - arm64)
; ExecutableFormat = Console
; CursorPosition = 104
; FirstLine = 54
; Folding = -
; EnableXP
; DPIAware