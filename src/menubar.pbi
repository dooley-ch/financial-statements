;╔═════════════════════════════════════════════════════════════════════════════════════════════════
;║     menubar.pbi                                                                           
;╠═════════════════════════════════════════════════════════════════════════════════════════════════
;║     Created: 09-08-2025 
;║
;║     Copyright (c) 2025 James Dooley <james@dooley.ch>
;║
;║     History:
;║     09-08-2025: Initial version
;╚═════════════════════════════════════════════════════════════════════════════════════════════════
DeclareModule MenuBarModule
  
  Declare.i CreateAppMenuBar(wndId.i) ; Creates the menubar 
  
  Declare.s MenuBarModuleError() ; Returns last error generated by the library

EndDeclareModule

Module MenuBarModule
  EnableExplicit
  
  UseModule Consts
  
  UsePNGImageDecoder()
  
  Define lastError$ = ""
  Define.i hMainWindowId, hMenuBarId
  
  ;-------------------- Menu Event Handlers --------------------
  
  Procedure OnQuitClicked()
    Shared hMainWindowId
    PostEvent(#APP_EVENT_Quit, hMainWindowId, #PB_Ignore, #PB_Ignore)
  EndProcedure
  
  Procedure OnDownloadClicked()
    Shared hMainWindowId
    PostEvent(#APP_EVENT_Download, hMainWindowId, #PB_Ignore, #PB_Ignore)
  EndProcedure
  
  Procedure OnImportClicked()
    Shared hMainWindowId
    PostEvent(#APP_EVENT_Import, hMainWindowId, #PB_Ignore, #PB_Ignore)
  EndProcedure
  
  Procedure OnBuildClicked()
    Shared hMainWindowId
    PostEvent(#APP_EVENT_Build, hMainWindowId, #PB_Ignore, #PB_Ignore)
  EndProcedure
  
  Procedure OnPublishClicked()
    Shared hMainWindowId
    PostEvent(#APP_EVENT_Publish, hMainWindowId, #PB_Ignore, #PB_Ignore)
  EndProcedure
  
  Procedure OnSettingsClicked()
    Shared hMainWindowId
    PostEvent(#APP_EVENT_Setup, hMainWindowId, #PB_Ignore, #PB_Ignore)
  EndProcedure
  
  Procedure OnManualClicked()
    Shared hMainWindowId
    PostEvent(#APP_EVENT_Manual, hMainWindowId, #PB_Ignore, #PB_Ignore)
  EndProcedure
  
  Procedure OnAboutClicked()
    Shared hMainWindowId
    PostEvent(#APP_EVENT_About, hMainWindowId, #PB_Ignore, #PB_Ignore)
  EndProcedure
  
  ;┌───────────────────────────────────────────────────────────────────────────────────────────────
  ;│     Public     
  ;└───────────────────────────────────────────────────────────────────────────────────────────────

  ; Returns last error generated by the library
  Procedure.s MenuBarModuleError()
    Shared lastError$
    ProcedureReturn lastError$
  EndProcedure
  
  ; Creates the menubar 
  ;
  ; Params
  ; wndId - The main window id
  ;
  ; Returns: The id of the new menubar
  ;
  Procedure.i CreateAppMenuBar(hWindow.i)
    Shared hMainWindowId, hMenuBarId, lastError$
    Protected.i hFileQuitIcon, hActionDownloadIcon, hActionSettingsIcon, hHelpManualIcon, hHelpAboutIcon, hActionImportIcon, hActionBuildIcon, hActionPublishIcon

    lastError$ = #Empty$
    
    If IsWindow(hWindow)
      hMainWindowId = hWindow
      
      hFileQuitIcon = CatchImage(#PB_Any, ?FileQuitIcon)
      hActionDownloadIcon = CatchImage(#PB_Any, ?ActionDownloadIcon)
      hActionImportIcon = CatchImage(#PB_Any, ?ActionImportIcon)
      hActionBuildIcon = CatchImage(#PB_Any, ?ActionBuildIcon)
      hActionPublishIcon = CatchImage(#PB_Any, ?ActionPublishIcon)
      hActionSettingsIcon = CatchImage(#PB_Any, ?ActionSettingsIcon)
      hHelpManualIcon = CatchImage(#PB_Any, ?HelpManualIcon)
      hHelpAboutIcon = CatchImage(#PB_Any, ?HelpAboutIcon)
      
      hMenuBarId = CreateImageMenu(#PB_Any, WindowID(hMainWindowId))
      
      If IsMenu(hMenuBarId)
        MenuTitle("&File")
          CompilerIf #PB_Compiler_OS = #PB_OS_Windows
            MenuBar()
            MenuItem(#File_Quit, "&Quit"  + Chr(9) + "Cmd+Q", ImageID(hFileQuitIcon))
            BindMenuEvent(hMenuBarId, #File_Quit, @OnQuitClicked())
          CompilerEndIf
        
        MenuTitle("Action")
          MenuItem(#Action_Download, "&Download SimFin Files", ImageID(hActionDownloadIcon))
          MenuItem(#Action_Import, "&Import Data Files", ImageID(hActionImportIcon))
          MenuItem(#Action_Build, "&Build Database", ImageID(hActionBuildIcon))
          MenuItem(#Action_Publish, "&Publish Database", ImageID(hActionPublishIcon))
          CompilerIf #PB_Compiler_OS = #PB_OS_Windows
            MenuBar()
            MenuItem(#Action_Preferences, "&Settings", ImageID(hActionSettingsIcon))
            BindMenuEvent(hMenuBarId, #Action_Preferences, @OnSettingsClicked()) 
          CompilerEndIf
        
        MenuTitle("&Help")
          MenuItem(#Help_Manual, "&Manual", ImageID(hHelpManualIcon))
          CompilerIf #PB_Compiler_OS = #PB_OS_Windows
            MenuBar()
            MenuItem(#Help_About, "&About", ImageID(hHelpAboutIcon))
            BindMenuEvent(hMenuBarId, #Help_About, @OnAboutClicked())
          CompilerEndIf
          
        ; Activate the Mac application menu options  
        CompilerIf #PB_Compiler_OS = #PB_OS_MacOS
          MenuItem(#PB_Menu_Quit, "")
          MenuItem(#PB_Menu_About, "")
          MenuItem(#PB_Menu_Preferences, "")
          
          BindMenuEvent(hMenuBarId, #PB_Menu_About, @OnAboutClicked())
          BindMenuEvent(hMenuBarId, #PB_Menu_Preferences, @OnSettingsClicked())
          BindMenuEvent(hMenuBarId, #PB_Menu_Quit, @OnQuitClicked())
        CompilerEndIf
        
        ; Bind common events
        BindMenuEvent(hMenuBarId, #Action_Download, @OnDownloadClicked()) 
        BindMenuEvent(hMenuBarId, #Action_Import, @OnImportClicked())
        BindMenuEvent(hMenuBarId, #Action_Build, @OnBuildClicked())
        BindMenuEvent(hMenuBarId, #Action_Publish, @OnPublishClicked())

        BindMenuEvent(hMenuBarId, #Help_Manual, @OnManualClicked())        

        ProcedureReturn hMenuBarId
      EndIf
    EndIf
    
    lastError$ = "Invalid window ID passed to CreateAppMenuBar function"
    ProcedureReturn 0
  EndProcedure
  
  DataSection
    FileQuitIcon:
    IncludeBinary #PB_Compiler_FilePath + "res/toolbar-menu/exit@16px.png"
    
    ActionDownloadIcon:
    IncludeBinary #PB_Compiler_FilePath + "res/toolbar-menu/download@16px.png"
        
    ActionImportIcon:
    IncludeBinary #PB_Compiler_FilePath + "res/toolbar-menu/import@16px.png"
    
    ActionBuildIcon:
    IncludeBinary #PB_Compiler_FilePath + "res/toolbar-menu/build@16px.png"
    
    ActionPublishIcon:
    IncludeBinary #PB_Compiler_FilePath + "res/toolbar-menu/publish@16px.png"
    
    ActionSettingsIcon:
    IncludeBinary #PB_Compiler_FilePath + "res/toolbar-menu/settings@16px.png"
    
    HelpManualIcon:
    IncludeBinary #PB_Compiler_FilePath + "res/toolbar-menu/manual@16px.png"
    
    HelpAboutIcon:
    IncludeBinary #PB_Compiler_FilePath + "res/toolbar-menu/about@16px.png"    
  EndDataSection
EndModule
; IDE Options = PureBasic 6.21 - C Backend (MacOS X - arm64)
; ExecutableFormat = Console
; CursorPosition = 145
; FirstLine = 131
; Folding = ---
; EnableXP
; DPIAware