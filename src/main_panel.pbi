;╔═════════════════════════════════════════════════════════════════════════════════════════════════
;║     main_panel.pbi                                                                           
;╠═════════════════════════════════════════════════════════════════════════════════════════════════
;║     Created: 10-08-2025 
;║
;║     Copyright (c) 2025 James Dooley <james@dooley.ch>
;║
;║     History:
;║     10-08-2025: Initial version
;╚═════════════════════════════════════════════════════════════════════════════════════════════════
DeclareModule MainPanelUI
  
  Declare.i CreateMainPanel(hWindowId.i) ; Creates the main panel
  
  Declare.b SelectDownloadPanel() ; Select the required panel
  Declare.b SelectImportPanel()   ; Select the required panel
  Declare.b SelectBuildPanel()    ; Select the required panel
  Declare.b SelectPublishPanel()  ; Select the required panel
  
  Declare.s MainPanelUIError() ; Returns last error generated by the library

EndDeclareModule

Module MainPanelUI
  EnableExplicit
  
  UseModule Consts
  UseModule SplashPanelUI
  UseModule DownloadPanelUI
  UseModule ImportPanelUI
  UseModule BuildPanelUI
  UseModule PublishPanelUI
  
  Define lastError$ = ""
  Define.i hMainWindowId, hMainPanelId
  Define.i hCurrentPanel, hSplashPanel, hDownloadPanel, hImportPanel, hBuildPanel, hPublishPanel
  
  Procedure OnResizePanel()
    Shared hMainPanelId, hCurrentPanel
    Protected panelWidth.i, panelHeight.i
    
    panelWidth = GadgetWidth(hMainPanelId, #PB_Gadget_ActualSize)
    panelHeight = GadgetHeight(hMainPanelId, #PB_Gadget_ActualSize)
    
    If IsGadget(hCurrentPanel)
      ResizeGadget(hCurrentPanel, 0, 0, panelWidth, panelHeight)
    EndIf
  EndProcedure
  
  ;┌───────────────────────────────────────────────────────────────────────────────────────────────
  ;│     Public     
  ;└───────────────────────────────────────────────────────────────────────────────────────────────

  ; Returns last error generated by the library
  Procedure.s MainPanelUIError()
    Shared lastError$
    ProcedureReturn lastError$
  EndProcedure
  
  ; Creates the main panel 
  ;
  ; Params
  ; wndId - The main window id
  ;
  ; Returns: The id of the new panel
  ;
  Procedure.i CreateMainPanel(hWindowId.i)
    Shared hMainWindowId, hMainPanelId, hSplashPanel, hDownloadPanel, hImportPanel, hBuildPanel, hPublishPanel, hCurrentPanel, lastError$
    
    lastError$ = #Empty$
    
    If IsWindow(hWindowId)
      hMainWindowId = hWindowId
      
      hMainPanelId = ContainerGadget(#PB_Any, 300, 300, 300, 300, #PB_Container_BorderLess)
      If IsGadget(hMainPanelId)
          hSplashPanel = CreateSplashPanel(hMainWindowId)
          hDownloadPanel = CreateDownloadPanel(hMainWindowId)
          hImportPanel = CreateImportPanel(hMainWindowId)
          hBuildPanel = CreateBuildPanel(hMainWindowId)
          hPublishPanel = CreatePublishPanel(hMainWindowId)
        CloseGadgetList()
        
        HideGadget(hDownloadPanel, #True)
        HideGadget(hImportPanel, #True)
        HideGadget(hBuildPanel, #True)
        HideGadget(hPublishPanel, #True)
        
        hCurrentPanel = hSplashPanel
        
        SetGadgetColor(hMainPanelId, #PB_Gadget_BackColor, MAIN_PANEL_Background)
        
        BindGadgetEvent(hMainPanelId, @OnResizePanel(), #PB_EventType_Resize)
        
        ProcedureReturn hMainPanelId
      EndIf
      
      lastError$ = "Failed to create main panel, invalid ID returned"
      ProcedureReturn 0   
    EndIf
    
    lastError$ = "Invalid window ID passed to CreateMainPanel function"
    ProcedureReturn 0    
  EndProcedure
  
  ; Select the required panel
  Procedure.b SelectDownloadPanel() 
    Shared hCurrentPanel, hDownloadPanel
    
    HideGadget(hCurrentPanel, #True)
    
    hCurrentPanel = hDownloadPanel
    
    HideGadget(hCurrentPanel, #False)
    OnResizePanel()
  EndProcedure
  
  ; Select the required panel
  Procedure.b SelectImportPanel() 
    Shared hCurrentPanel, hImportPanel
    
    HideGadget(hCurrentPanel, #True)
    
    hCurrentPanel = hImportPanel
    
    HideGadget(hCurrentPanel, #False)
    OnResizePanel()  
  EndProcedure
  
  ; Select the required panel
  Procedure.b SelectBuildPanel()
    Shared hCurrentPanel, hBuildPanel
    
    HideGadget(hCurrentPanel, #True)
    
    hCurrentPanel = hBuildPanel
    
    HideGadget(hCurrentPanel, #False)
    OnResizePanel()      
  EndProcedure
  
  ; Select the required panel
  Procedure.b SelectPublishPanel() 
    Shared hCurrentPanel, hPublishPanel
    
    HideGadget(hCurrentPanel, #True)
    
    hCurrentPanel = hPublishPanel
    
    HideGadget(hCurrentPanel, #False)
    OnResizePanel()     
  EndProcedure
EndModule
; IDE Options = PureBasic 6.21 - C Backend (MacOS X - arm64)
; ExecutableFormat = Console
; CursorPosition = 89
; FirstLine = 67
; Folding = --
; EnableXP
; DPIAware