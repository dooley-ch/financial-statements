;╔═════════════════════════════════════════════════════════════════════════════════════════════════
;║     setup_up.pbi                                                                         
;╠═════════════════════════════════════════════════════════════════════════════════════════════════
;║     Created: 10-08-2025 
;║
;║     Copyright (c) 2025 James Dooley <james@dooley.ch>
;║
;║     History:
;║     10-08-2025: Initial version
;╚═════════════════════════════════════════════════════════════════════════════════════════════════
DeclareModule SetupUI
  
  Declare.b OpenSettingsDialog(hWindow.i) ; Open's the setup dialog
  
  Declare.s SetupUIError() ; Returns last error generated by the library

EndDeclareModule

Module SetupUI
  EnableExplicit
  
  UsePNGImageDecoder()
  
  UseModule Consts
  UseModule SetupModule
  
  #Dlg_Width = 350
  #Dlg_Height = 415
  
  #Dlg_Label_Width = 75
  
  #Dlg_Frame_Height_DataFolder = 60
  #Dlg_Frame_Height_SimFinAccount = 120
  #Dlg_Frame_Height_DatabaseLogin = 185
  
  #Dlg_Frame_X1 = #DLG_DEFAULT_GADGET_PADDING
  #Dlg_Frame_X2 = #Dlg_Frame_X1 + #Dlg_Label_Width + #DLG_DEFAULT_GADGET_PADDING
  
  #Dlg_Frame_Y1 = (#DLG_DEFAULT_GADGET_PADDING * 6) + 2
  #Dlg_Frame_Y2 = #Dlg_Frame_Y1 + #DLG_DEFAULT_BUTTON_HEIGHT + #DLG_DEFAULT_GADGET_PADDING
  #Dlg_Frame_Y3 = #Dlg_Frame_Y2 + #DLG_DEFAULT_BUTTON_HEIGHT + #DLG_DEFAULT_GADGET_PADDING
  #Dlg_Frame_Y4 = #Dlg_Frame_Y3 + #DLG_DEFAULT_BUTTON_HEIGHT + #DLG_DEFAULT_GADGET_PADDING
  #Dlg_Frame_Y5 = #Dlg_Frame_Y4 + #DLG_DEFAULT_BUTTON_HEIGHT + #DLG_DEFAULT_GADGET_PADDING
  
  #Dlg_Column_X1 = #DLG_DEFAULT_GADGET_PADDING ; Frame X
  #Dlg_Column_Y1 = (#DLG_DEFAULT_GADGET_PADDING * 2) ; Data Frame Y
  #Dlg_Column_Y2 = #Dlg_Column_Y1 + #Dlg_Frame_Height_DataFolder + #DLG_DEFAULT_GADGET_PADDING ; SimFin Frame Y
  #Dlg_Column_Y3 = #Dlg_Column_Y2 + #Dlg_Frame_Height_SimFinAccount + #DLG_DEFAULT_GADGET_PADDING ; Database Login Y
  
  #Dlg_Frame_Width = #Dlg_Width - (#Dlg_Column_X1 * 2)
  #Dlg_InFrame_Width = #Dlg_Frame_Width - (#DLG_DEFAULT_GADGET_PADDING * 5) - 3
  
  #Dlg_TextBox_Width = #Dlg_InFrame_Width - #Dlg_Frame_X2
  #Dlg_TextBox_Width_Short = (#Dlg_TextBox_Width * 0.25)
  
  #Dlg_DataFolder_Width = #Dlg_InFrame_Width - #DLG_DEFAULT_BUTTON_HEIGHT
  #Dlg_Frame_X3 = #Dlg_Frame_X1 + #Dlg_DataFolder_Width
  
  #Dlg_Cancel_X = #Dlg_Width - #DLG_DEFAULT_BUTTON_WIDTH - #DLG_DEFAULT_GADGET_PADDING 
  #Dlg_OK_X = #Dlg_Cancel_X - #DLG_DEFAULT_BUTTON_WIDTH - #DLG_DEFAULT_GADGET_PADDING
  #Dlg_Buttons_Y = #Dlg_Height - #DLG_DEFAULT_BUTTON_HEIGHT- #DLG_DEFAULT_GADGET_PADDING
  
  Define lastError$ = ""
  Define.i hMainWindow, hDialog, hOkButton, hBrowseButton
  Define.i hDataFolder, hSimFinUrl, hSimFinKey, hSimMaxCalls, hDatabase, hHost, hPort, hUser, hPassword
  
  Procedure.b CopyDialogData(*record.UserEditSettingsRecord)
    Shared lastError$, hDataFolder, hSimFinUrl, hSimFinKey, hSimMaxCalls, hDatabase, hHost, hPort, hUser, hPassword
    
    With *record
      \dataFolder = Trim(GetGadgetText(hDataFolder))
      \simFinUrl = Trim(GetGadgetText(hSimFinUrl))
      \simFinKey = Trim(GetGadgetText(hSimFinKey))
      \simFinMinCalls = Val(Trim(GetGadgetText(hSimMaxCalls)))
      \dbName = Trim(GetGadgetText(hDatabase))
      \dbHost = Trim(GetGadgetText(hHost))
      \dbPort = Val(Trim(GetGadgetText(hPort)))
      \dbUserId = Trim(GetGadgetText(hUser))
      \dbPassword = Trim(GetGadgetText(hPassword))
    EndWith
    
    ProcedureReturn #True
  EndProcedure
  
  ; ---------- Event Handlers ----------
  
  Procedure CloseDialog()
    Shared hMainWindow, hDialog
    
    If IsWindow(hDialog)
      CloseWindow(hDialog)
    EndIf
    
    DisableWindow(hMainWindow, #False)
    SetActiveWindow(hMainWindow)
  EndProcedure
  
  Procedure OnCloseDialog()
    CloseDialog()
  EndProcedure
  
  Procedure OnOkClicked()
    Protected record.UserEditSettingsRecord
    
    If CopyDialogData(@record)
      StoreUserEditSettingsRecord(@record)
      CloseDialog()
    EndIf

  EndProcedure
  
  Procedure OnValidateInput()
    Shared hOkButton
    Protected record.UserEditSettingsRecord, disableOK = #True
    
    If CopyDialogData(@record)
      With record
        If (\dataFolder <> #Empty$) And (Len(\dataFolder) > 8)                  ; Validate the data folder
          If (\simFinUrl <> #Empty$) And (Len(\simFinUrl) > 20)                 ; Validate the SimFin URL
            If (\simFinKey <> #Empty$) And (Len(\simFinKey) >= 10)              ; Validate the SimFin Key
              If \simFinMinCalls > 0                                            ; Validate the SimFin Max calls number
                If (\dbName <> #Empty$) And (Len(\dbName) >= 10)                ; Validate the database name
                  If (\dbHost <> #Empty$) And (Len(\dbHost) >= 8)               ; Validate the host name
                    If \dbPort > 0                                              ; Validate the port number
                      If (\dbUserId <> #Empty$) And (Len(\dbUserId) > 4)        ; Validate the database user id
                        If (\dbPassword <> #Empty$) And (Len(\dbPassword) > 6)  ; Validate the database password
                          disableOK = #False
                        EndIf
                      EndIf
                    EndIf
                  EndIf
                EndIf
              EndIf
            EndIf
          EndIf
        EndIf
      EndWith
    EndIf
    
    DisableGadget(hOKButton, disableOK)
  EndProcedure
  
  Procedure OnBrowseFolderClicked()
    Shared hDataFolder, hSimFinUrl
    Protected selectedFolder$ = Trim(GetGadgetText(hDataFolder))
    
    If Len(selectedFolder$) = 0
      selectedFolder$ = GetUserDirectory(#PB_Directory_Documents)
    EndIf
    
    selectedFolder$ = Trim(PathRequester("Select base data folder", selectedFolder$))
    If selectedFolder$
      SetGadgetText(hDataFolder, selectedFolder$)
      SetActiveGadget(hSimFinUrl)
      OnValidateInput()
    EndIf
  EndProcedure
  
  ;┌───────────────────────────────────────────────────────────────────────────────────────────────
  ;│     Public     
  ;└───────────────────────────────────────────────────────────────────────────────────────────────

  ; Returns last error generated by the library
  Procedure.s SetupUIError()
    Shared lastError$
    ProcedureReturn lastError$
  EndProcedure
  
  ; Open's the setup dialog  
  ;
  ; Params
  ; hWindow - The ID of the application's main window
  ;
  Procedure.b OpenSettingsDialog(hWindow.i) 
    Shared lastError$, hMainWindow, hDialog, hDataFolder, hSimFinUrl, hSimFinKey, 
           hSimMaxCalls, hDatabase, hHost, hPort, hUser, hPassword, hBrowseButton, hOkButton
    Protected hLogo.i, hCancelButton.i, hFrame.i, hBrowseImage.i, record.UserEditSettingsRecord
    lastError$ = #Empty$
    
    If IsWindow(hWindow)
      hMainWindow = hWindow
      
      GetUserEditSettingsRecord(@record)
      
      hDialog = OpenWindow(#PB_Any, #PB_Default, #PB_Default, #Dlg_Width, #Dlg_Height, "Settings", #DLG_WINDOW_FLAGS, WindowID(hMainWindow))
      If IsWindow(hDialog)
        hBrowseImage = CatchImage(#PB_Any, ?BrowseButtonIcon)
        
        hFrame = FrameGadget(#PB_Any, #Dlg_Column_X1, #Dlg_Column_Y1, #Dlg_Frame_Width, #Dlg_Frame_Height_DataFolder, "Base Data Folder", #PB_Frame_Container | #PB_Frame_Single)
          hDataFolder = StringGadget(#PB_Any, #Dlg_Frame_X1, #Dlg_Frame_Y1, #Dlg_DataFolder_Width, #DLG_DEFAULT_BUTTON_HEIGHT, record\dataFolder, #PB_String_ReadOnly)
          hBrowseButton = ButtonImageGadget(#PB_Any, #Dlg_Frame_X3, #Dlg_Frame_Y1 - 1, #DLG_DEFAULT_IMAGE_BUTTON_WIDTH, #DLG_DEFAULT_IMAGE_BUTTON_HEIGHT, ImageID(hBrowseImage))
        CloseGadgetList()
        
        hFrame = FrameGadget(#PB_Any, #Dlg_Column_X1, #Dlg_Column_Y2, #Dlg_Frame_Width, #Dlg_Frame_Height_SimFinAccount, "SimFin Account", #PB_Frame_Container | #PB_Frame_Single)
          TextGadget(#PB_Any, #Dlg_Frame_X1, #Dlg_Frame_Y1 + 5, #Dlg_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, "URL:")
          TextGadget(#PB_Any, #Dlg_Frame_X1, #Dlg_Frame_Y2 + 5, #Dlg_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, "Key:")
          TextGadget(#PB_Any, #Dlg_Frame_X1, #Dlg_Frame_Y3 + 5, #Dlg_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, "Max. Calls:")
       
          hSimFinUrl = StringGadget(#PB_Any, #Dlg_Frame_X2, #Dlg_Frame_Y1, #Dlg_TextBox_Width, #DLG_DEFAULT_BUTTON_HEIGHT, record\simFinUrl)
          hSimFinKey = StringGadget(#PB_Any, #Dlg_Frame_X2, #Dlg_Frame_Y2, #Dlg_TextBox_Width, #DLG_DEFAULT_BUTTON_HEIGHT, record\simFinKey)
          hSimMaxCalls = StringGadget(#PB_Any, #Dlg_Frame_X2, #Dlg_Frame_Y3, #Dlg_TextBox_Width_Short, #DLG_DEFAULT_BUTTON_HEIGHT, Str(record\simFinMinCalls), #PB_String_Numeric)
        CloseGadgetList()
        
        hFrame = FrameGadget(#PB_Any, #Dlg_Column_X1, #Dlg_Column_Y3, #Dlg_Frame_Width, #Dlg_Frame_Height_DatabaseLogin, "Database Login", #PB_Frame_Container | #PB_Frame_Single)
          TextGadget(#PB_Any, #Dlg_Frame_X1, #Dlg_Frame_Y1 + 5, #Dlg_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, "Database:")
          TextGadget(#PB_Any, #Dlg_Frame_X1, #Dlg_Frame_Y2 + 5, #Dlg_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, "Host:")
          TextGadget(#PB_Any, #Dlg_Frame_X1, #Dlg_Frame_Y3 + 5, #Dlg_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, "Port:")
          TextGadget(#PB_Any, #Dlg_Frame_X1, #Dlg_Frame_Y4 + 5, #Dlg_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, "User ID:")
          TextGadget(#PB_Any, #Dlg_Frame_X1, #Dlg_Frame_Y5 + 5, #Dlg_Label_Width, #DLG_DEFAULT_BUTTON_HEIGHT, "Password:")  
     
          hDatabase = StringGadget(#PB_Any, #Dlg_Frame_X2, #Dlg_Frame_Y1, #Dlg_TextBox_Width, #DLG_DEFAULT_BUTTON_HEIGHT, record\dbName)
          hHost = StringGadget(#PB_Any, #Dlg_Frame_X2, #Dlg_Frame_Y2, #Dlg_TextBox_Width, #DLG_DEFAULT_BUTTON_HEIGHT, record\dbHost)
          hPort = StringGadget(#PB_Any, #Dlg_Frame_X2, #Dlg_Frame_Y3, #Dlg_TextBox_Width_Short, #DLG_DEFAULT_BUTTON_HEIGHT, Str(record\dbPort), #PB_String_Numeric)  
          hUser = StringGadget(#PB_Any, #Dlg_Frame_X2, #Dlg_Frame_Y4, #Dlg_TextBox_Width, #DLG_DEFAULT_BUTTON_HEIGHT, record\dbUserId)
          hPassword = StringGadget(#PB_Any, #Dlg_Frame_X2, #Dlg_Frame_Y5, #Dlg_TextBox_Width, #DLG_DEFAULT_BUTTON_HEIGHT, record\dbPassword, #PB_String_Password)           
        CloseGadgetList()
        
        hOKButton = ButtonGadget(#PB_Any, #Dlg_OK_X, #Dlg_Buttons_Y, #DLG_DEFAULT_BUTTON_WIDTH, #DLG_DEFAULT_BUTTON_HEIGHT, "Ok", #PB_Button_Default)
        hCancelButton = ButtonGadget(#PB_Any, #Dlg_Cancel_X, #Dlg_Buttons_Y, #DLG_DEFAULT_BUTTON_WIDTH, #DLG_DEFAULT_BUTTON_HEIGHT, "Cancel")
        
        BindGadgetEvent(hBrowseButton, @OnBrowseFolderClicked(), #PB_EventType_LeftClick)
        
        BindGadgetEvent(hDataFolder, @OnValidateInput(), #PB_EventType_Change)
        
        BindGadgetEvent(hSimFinUrl, @OnValidateInput(), #PB_EventType_Change)
        BindGadgetEvent(hSimFinKey, @OnValidateInput(), #PB_EventType_Change)
        BindGadgetEvent(hSimMaxCalls, @OnValidateInput(), #PB_EventType_Change)
        
        BindGadgetEvent(hDatabase, @OnValidateInput(), #PB_EventType_Change)
        BindGadgetEvent(hHost, @OnValidateInput(), #PB_EventType_Change)
        BindGadgetEvent(hPort, @OnValidateInput(), #PB_EventType_Change)
        BindGadgetEvent(hUser, @OnValidateInput(), #PB_EventType_Change)
        BindGadgetEvent(hPassword, @OnValidateInput(), #PB_EventType_Change)
        
        BindGadgetEvent(hOKButton, @OnOkClicked(), #PB_EventType_LeftClick)
        DisableGadget(hOKButton, #True)
        
        BindGadgetEvent(hCancelButton, @OnCloseDialog(), #PB_EventType_LeftClick)
        BindEvent(#PB_Event_CloseWindow, @OnCloseDialog(), hDialog)
        
        ; Stop processing on the main window
        DisableWindow(hMainWindow, #True)
        
        SetActiveGadget(hBrowseButton)
        
        ProcedureReturn #True
      EndIf
      
      lastError$ = "Failed to create Setup dialog window"
      ProcedureReturn #False      
    EndIf
    
    lastError$ = "Invalid window ID passed to function: OpenSettingsDialog"
    ProcedureReturn #False
  EndProcedure
  
  DataSection
    BrowseButtonIcon:
    IncludeBinary #PB_Compiler_FilePath + "res/buttons/browse-folder@16px.png"  
  EndDataSection

EndModule
; IDE Options = PureBasic 6.21 - C Backend (MacOS X - arm64)
; ExecutableFormat = Console
; CursorPosition = 134
; FirstLine = 119
; Folding = --
; EnableXP
; DPIAware